multi_model LeaseCompany

model Cars

class Car
attributes
  price : Integer
  serial_number : Integer
end

class GasCar < Car
end

class ElectricCar < Car
attributes
  battry_capacity : Integer
end


model Customers

class Customer
attributes
  name : String
  is_new_driver : Boolean
end

class BuisnessCustomer < Customer
attributes
  company_name : String
end


model Workers

class Worker
attributes
  name : String
end

class Department
attributes
  name : String
end

association workInDepratment between
  Worker[1..*] role worker
  Department[1] role department
end

association supervisedBy between
  Worker[0..1] role supervisor
  Worker[0..*] role workers
end

constraints
context Worker inv ManagerNotTransitive:
  not Worker.allInstances()->exists(w | 
    w.supervisor->notEmpty() and w.supervisor.supervisor->notEmpty() and
    w.supervisor.supervisor = self)

inter-classes

class RentalAgreement
attributes
  start_date : Integer
  end_date : Integer
end

inter-associations

association leasedBy between
	RentalAgreement[1..*] role agreement
	Customers@Customer[1] role customer
end
 
association mediateBy between
	RentalAgreement[0..*] role agreement
	Workers@Worker[1] role soldby
end

association leasedCar between
	RentalAgreement[0..*] role agreement
	Cars@Car[1] role car
end


association permissionToDrive between
	Customers@Customer[0..*] role driver
	Cars@Car[0..*] role car
end


inter-constraints

context RentalAgreement

  --a car cannot be rented by two customers at the same time
  inv SameCar:
    RentalAgreement.allInstances()->forAll(r1, r2 | (r1 <> r2 and r1.car.serial_number = r2.car.serial_number)
    implies (r1.end_date < r2.start_date)  or (r2.end_date < r1.start_date) )

  --only simple workers can make sells
  inv ManagerCannotSell:
    self.soldby.workers->size() = 0 and self.soldby.supervisor <> null

context Cars@Car
  inv NewDriverCannotDriveElectric:
    self.oclIsKindOf(Cars@ElectricCar) implies self.driver->forAll(d1 | d1.is_new_driver = false)



