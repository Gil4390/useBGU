MLM ABCD

model Computer_product
    class Hardware
      attributes
        testDate: Integer
    end

    class Computer < Hardware
      attributes
        maker: String
        relatedStandard: String
    end

    class Peripheral < Hardware
    end

    class Software
    end

    class System < Software
      attributes
        protected: Boolean
    end

    class Application < Software
    end

    association hardwSoftw between
      Hardware[0..*] role hardw
      Software[0..*] role softw
    end

    association compatibility between
      System[1..*] role syst
      Application[1..*] role appl
    end

    association contain between
      Hardware[0..*] role part
      Hardware[0..*] role parent
    end

    constraints
    context Application inv ApplSysCompat:
      self.syst.hardw->includesAll(self.hardw)

model PC
    class Device
      attributes
        manuDate: Integer
    end

    class PC
    end

    class Desktop < PC
    end

    class Laptop < PC
    end

    class PCAppl
    end

    class PCOS
      attributes
        openSource: Boolean
    end

    association pcCon between
      Device[0..*] role part
      PC[0..*] role parent
    end

    association pcAppl between
      PC[0..*] role hardw
      PCAppl[0..*] role softw
    end

    association pcCompat between
      PCOS[0..*] role syst
      PCAppl[0..*] role appl
    end

    association pcOs between
      PC[0..*] role pc
      PCOS[1..*] role os
    end

--    constraints
--    context Device inv DeviceDates:
--      self.parent.testDate->forAll(d| d > manuDate)

inter-associations
association connection between
    Computer_product@Computer[1] role handler
    PC@PC[1] role maintained
end


association installation between
    Computer_product@Application[1] role installer
    PC@PCOS[1..*] role installed
end

inter-constraints
context Computer_product@Application inv PCInstallation:
    syst.hardw->
    select(h | h.oclIsTypeOf(Computer_product@Computer) )->
    collect(c | c.oclAsType(Computer_product@Computer).maintained)->includesAll(self.installed.pc)

mediator Computer_product < NONE
end

mediator PC < Computer_product

   clabject Device : Peripheral
   end

   clabject PC : Computer
        attributes
            ~relatedStandard
   end

   clabject PCOS : System
   end

   clabject PCAppl : Application
   end

   assoclink pcCon : contain
        parent->parent
        part->part
   end

   assoclink pcAppl : hardwSoftw
        softw->softw
        hardw->hardw
   end

   assoclink pcOs : hardwSoftw
        os->softw
        pc->hardw
   end

   assoclink pcCompat : compatibility
        syst->syst
        appl->appl
   end

end
